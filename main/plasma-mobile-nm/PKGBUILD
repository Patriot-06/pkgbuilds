# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
_mode=host
pkgname=plasma-mobile-nm
_pkgname=plasma-nm
pkgdesc="Plasma applet written in QML for managing network connections - Mobile Edition"
pkgver=5.26.5.r0.ge507aa40
pkgrel=1
_arches=all
arch=(
    aarch64
    x86_64
    armv7h
)
license=(GPL-2.0-or-later)
provides=(
    "${_pkgname}"
)
conflicts=(
    "${_pkgname}"
)
depends=(
    plasma-workspace
    modemmanager-qt
    networkmanager-qt
    qca-qt5
    mobile-broadband-provider-info
)
makedepends=(
    extra-cmake-modules
    openconnect
)
optdepends=("openconnect: Cisco AnyConnect VPN plugin")
_commit=e507aa404409909d45c862f9c579f50563466eeb
source=(
    "git+https://invent.kde.org/plasma/plasma-nm.git#commit=$_commit"
)
sha256sums=(
    SKIP
)

pkgver() {
    cd "${_pkgname}"
    git describe --long "$_commit" | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    cmake -B build -S $_pkgname \
        -DBUILD_TESTING=OFF \
        -DBUILD_MOBILE=True
    cmake --build build
}

package() {
    DESTDIR="$pkgdir" cmake --install build
}
