# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: Gustavo Castro < gustawho [ at ] gmail [ dot ] com >
# Contributor: Devin Lin <devin@kde.org>
_mode=host
pkgname=koko
pkgdesc="Image gallery application designed for desktop and touch devices"
pkgver=23.01.0.r0.ge3a4310
pkgrel=1
_arches=all
arch=(
    x86_64
    aarch64
    armv7h
)
license=(LGPL-2.1-or-later)
depends=(
    kirigami2
    kcoreaddons
    kguiaddons
    kconfig
    kquickimageeditor
    knotifications
    exiv2
    qt5-location
    kdeclarative
    kfilemetadata
    ki18n
    kio
    kdbusaddons
)
makedepends=(
    extra-cmake-modules
    qt5-svg
    qt5-tools
)
_commit=e3a4310e596fe6b0aad15caa89c218c44bba10b8
source=(
    "git+https://invent.kde.org/graphics/koko.git#commit=$_commit"
)
sha256sums=(
    SKIP
)

pkgver() {
    cd "$pkgname"
    git describe --long "$_commit" | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -B build -S "${pkgname}"
    cmake --build build --config Release
}

package() {
    DESTDIR="${pkgdir}" cmake --install build --config Release
}
