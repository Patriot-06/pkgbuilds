_mode=cross
_pkgname="qrtr"
pkgname="$_pkgname-git"
pkgdesc="Userspace reference for net/qrtr in the Linux kernel"
pkgver=r93.9dc7a88
pkgrel=1
arch=("aarch64")
url="https://github.com/andersson/$_pkgname"
license=("BSD-3-Clause")
groups=("qcom-icnss-wlan")
depends=("glibc")
makedepends=("git" "linux-headers")
provides=("$_pkgname")
_commit=9dc7a88548c27983e06465d3fbba2ba27d4bc050
source=("git://github.com/andersson/$_pkgname.git#commit=$_commit")
md5sums=("SKIP")

pkgver() {
	cd "$_pkgname"

	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd "$_pkgname"

	make prefix=/usr all
}

package() {
	cd "$_pkgname"

	make prefix=/usr DESTDIR="$pkgdir/" install
	install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$_pkgname/COPYING
}
