# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>
#
# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable x86 extensions (mmx, sse2, ssse3)
#
# Kupfer: enable aarch64 vector instructions
_mode=host
pkgname=pixman-kupfer
pkgdesc="The pixel-manipulation library for X and cairo"
pkgver=0.42.2
pkgrel=1
_arches=specific
arch=(
    aarch64
)
license=(custom)
url=https://gitlab.freedesktop.org/pixman/pixman/
provides=(
    libpixman-1.so
    pixman
)
conflicts=(pixman)
replaces=(pixman)
depends=(glibc)
makedepends=(
    meson
    libpng
)
source=(
    https://xorg.freedesktop.org/releases/individual/lib/pixman-${pkgver}.tar.xz
)
sha256sums=(
    5747d2ec498ad0f1594878cc897ef5eb6c29e91c53b899f7f71b506785fc1376
)

#validpgpkeys=('') # Maarten Lankhorst <maarten.lankhorst@linux.intel.com>

build() {
  cd "$srcdir/pixman-$pkgver"
  arch-meson build \
    -D loongson-mmi=disabled \
    -D vmx=disabled \
    -D arm-simd=disabled \
    -D neon=disabled \
    -D a64-neon=enabled \
    -D iwmmxt=disabled \
    -D mmx=disabled \
    -D sse2=disabled \
    -D ssse3=disabled \
    -D mips-dspr2=disabled \
    -D gtk=disabled
  ninja -C build
}

check() {
  #  meson test -C build
  true
}

package() {
  cd "$srcdir/pixman-$pkgver"
  DESTDIR="$pkgdir" meson install -C build
  install -Dt "$pkgdir/usr/share/licenses/pixman" -m644 COPYING
}
