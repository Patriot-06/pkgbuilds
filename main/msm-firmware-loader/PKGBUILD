_mode=cross
_nodeps=true
pkgname=msm-firmware-loader
pkgdesc="Set of init services to automatically load firmware from device partitions"
pkgver=1.3.0
pkgrel=0
_arches=specific
arch=(
    aarch64
    armhf
    armv7
)
license=(MIT)
install=$pkgname.install

source=(
    https://gitlab.com/postmarketOS/msm-firmware-loader/-/archive/$pkgver/msm-firmware-loader-$pkgver.tar.gz
    msm-firmware-loader.service
    msm-firmware-loader-unpack.service
    remoteproc-reloader.sh
    wifi-powersave.conf
    10-network.rules
)
sha256sums=(
    68969f6411d96194a323f00b28d2dd4053360e284cea04d26a9b06ea4c311fb4
    7c1060b76ed612a4300a496c12df773f9a775425a368c007fabaabcefe80d18f
    b40cedb81a3e0cde46eefe7a43e4b79c12ed5f68591ff2e09e5458f26ad2c6d7
    cfae5490913165dd1a2ee44c874514700d001e6e808ce067aa56c7f92fabeb55
    8919a1f7a8ab8c8a11c89e6046fe9141bab1a21cb3dcd7add7bca22248a8b564
    1ad2e717f8ade93c9c4e3b45b9a34f8cdee600e3436b256e8c540119a6f99fb1
)

package() {
    mkdir -p "$pkgdir"
    
    install -Dm755 msm-firmware-loader.service \
        "$pkgdir/etc/systemd/system/msm-firmware-loader.service"
    install -Dm755 msm-firmware-loader-unpack.service \
        "$pkgdir/etc/systemd/system/msm-firmware-loader-unpack.service"
    
    # Create mountpoint for the scripts
    mkdir -p "$pkgdir/usr/lib/firmware/msm-firmware-loader"
    
    install -Dm755 "$srcdir/msm-firmware-loader-$pkgver/msm-firmware-loader.sh" \
        "$pkgdir/usr/bin/msm-firmware-loader.sh"
    install -Dm755 "$srcdir/msm-firmware-loader-$pkgver/msm-firmware-loader-unpack.sh" \
        "$pkgdir/usr/bin/msm-firmware-loader-unpack.sh"
    install -Dm755 "$srcdir/remoteproc-reloader.sh" "$pkgdir/usr/bin/remoteproc-reloader.sh"
    
    # Turn off Wi-Fi powersave to improve wlan connection latency (200ms avg vs 2ms avg on local network)
    # https://gist.github.com/jcberthon/ea8cfe278998968ba7c5a95344bc8b55
    install -Dm755 "$srcdir/wifi-powersave.conf" "$pkgdir/etc/NetworkManager/conf.d/wifi-powersave.conf"
    
    # Set MTU=1380 to avoid ssh session hangups on huge transmissions
    install -Dm755 "$srcdir/10-network.rules" "$pkgdir/etc/udev/rules.d/10-network.rules"
}
