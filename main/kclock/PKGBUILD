# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: Bernhard Landauer <bernhard@manjaro.org>
# Contributor: Furkan K. <furkan@fkardame.com>
# Contributor: Bhushan Shah <bhush94 at gmail dot com>
# Contributor: Devin Lin <devin@kde.org>
_mode=host
pkgname=kclock
pkgdesc="Simple Clock application for Mobile Devices running Plasma"
pkgver=23.01.0.r0.gc59af0c
pkgrel=1
_arches=all
arch=(
    x86_64
    aarch64
    armv7h
)
license=(GPL-2.0-or-later)
depends=(
    kdbusaddons
    kirigami-addons
    knotifications
    plasma-framework
    qt5-quickcontrols2
    qt5-declarative
    qt5-multimedia
)
makedepends=(
    cmake
    extra-cmake-modules
    qt5-svg
)
_commit=c59af0ccd327908e6c69fa52894960838393c6aa
source=(
    "git+https://invent.kde.org/plasma-mobile/kclock.git#commit=$_commit"
)
sha256sums=(
    SKIP
)

pkgver() {
    cd "$pkgname"
    git describe --long "$_commit" | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir/" install
} 
