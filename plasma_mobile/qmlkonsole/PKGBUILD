# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: Dan Johansen <strit@manjaro.org>
# Contributor: Devin Lin <devin@kde.org>
_mode=host
pkgname=qmlkonsole
pkgdesc="Terminal app for Plasma Mobile"
pkgver=23.01.0.r0.g4d87b93
pkgrel=1
_arches=all
arch=(
    x86_64
    armv7h
    aarch64
)
license=(GPL-3.0-or-later)
depends=(
    qmltermwidget
    kirigami-addons
    kirigami2
    ki18n
    kconfig
    kcoreaddons
    kwindowsystem
)
makedepends=(
    extra-cmake-modules
    qt5-base
    qt5-declarative
    qt5-svg
    qt5-quickcontrols2
)
_commit=4d87b93cde9c030b9d01290bc7f2e2dc5ed5f73e
source=(
    "git+https://invent.kde.org/plasma-mobile/qmlkonsole.git#commit=$_commit"
)
sha256sums=(
    SKIP
)

pkgver() {
    cd "$pkgname"
    git describe --long --abbrev=7 --tags "$_commit" | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    mkdir -p build
    cd build
    cmake ${srcdir}/qmlkonsole \
        -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib
    make
}

package() {
    cd build
    DESTDIR="$pkgdir" make install
} 
