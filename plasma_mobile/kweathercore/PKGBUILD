# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: Gustavo Castro < gustawho [ at ] gmail [ dot ] com >
# Contributor: Devin Lin <devin@kde.org>
_mode=host
pkgname=kweathercore
pkgdesc="Library to facilitate retrieval of weather information including forecasts and alerts"
pkgver=0.7.r0.gabf5f29
pkgrel=1
_arches=all
arch=(
    x86_64
    armv7h
    aarch64
)
license=(GPL-3.0-or-later)
depends=(
    ki18n
    qt5-location
    kholidays
)
makedepends=(
    git
    extra-cmake-modules
    qt5-tools
)
_commit=abf5f293ef0b231447b83cb16571265649ad986e
source=(
    "git+https://invent.kde.org/libraries/kweathercore.git#commit=$_commit"
)
sha256sums=(
    SKIP
)

pkgver() {
    cd "$pkgname"
    git describe --long "$_commit" | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -B build -S $pkgname
    cmake --build build --config Release
}

package() {
    DESTDIR="${pkgdir}" cmake --install build --config Release
}
