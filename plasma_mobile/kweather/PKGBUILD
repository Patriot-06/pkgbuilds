# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: Bernhard Landauer <bernhard@manjaro.org>
# Contributor: Furkan K. <furkan@fkardame.com>
# Contributor: Bhushan Shah <bhush94 at gmail dot com>
# Contributor: Devin Lin <devin@kde.org>
_mode=host
pkgname=kweather
pkgdesc="Simple Weather application for Mobile Devices running Plasma"
pkgver=23.01.0.r0.gecb63d7
pkgrel=2
_arches=all
arch=(
    x86_64
    aarch64
    armv7h
)
license=(GPL-2.0-or-later)
depends=(
    kconfig
    kcoreaddons
    ki18n
    kirigami2
    kirigami-addons
    knotifications
    plasma-framework
    kquickcharts
    qt5-charts
    qt5-quickcontrols2
    qt5-declarative
    kweathercore
)
makedepends=(
    git
    cmake
    extra-cmake-modules
    qt5-svg
)
_commit=ecb63d775372db0ee750b4840dafc3082aedabb5
source=(
    "git+https://invent.kde.org/plasma-mobile/kweather.git#commit=$_commit"
)
sha256sums=(
    SKIP
)

pkgver() {
    cd "$pkgname"
    git describe --long --abbrev=7 --tags "$_commit" | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    mkdir build
}

build() {
    cd build
    cmake ../kweather \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib
    make
}

package() {
    cd build
    make DESTDIR="$pkgdir/" install
} 
