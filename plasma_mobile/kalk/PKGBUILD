# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: Bernhard Landauer <bernhard@manjaro.org>
# Contributor: Devin Lin <devin@kde.org>
_mode=host
pkgname=kalk
pkgdesc="Cross-platfrom calculator built with the Kirigami framework"
pkgver=23.01.0.r0.g6006abc
pkgrel=1
_arches=all
arch=(
    x86_64
    aarch64
    armv7h
)
license=(GPL-3.0-or-later)
depends=(
    ki18n
    kconfig
    kcoreaddons
    knotifications
    kirigami2
    kunitconversion
    kdbusaddons
    plasma-framework
    qt5-feedback
)
makedepends=(
    qt5-tools
    extra-cmake-modules
)
_commit=6006abc2942644229cfd78831cfe8f980be37568
source=(
    "git+https://invent.kde.org/plasma-mobile/kalk.git#commit=$_commit"
)
sha256sums=(
    SKIP
)

pkgver() {
    cd "$pkgname"
    git describe --long --abbrev=7 --tags "$_commit" | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    mkdir build
}

build() {
    cd build
    cmake ../$pkgname \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr
    make
}

package() {
    cd build
    make DESTDIR="$pkgdir" install
}
