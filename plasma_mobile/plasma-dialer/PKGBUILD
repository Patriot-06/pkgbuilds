# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: Furkan Kardame <tech@fkardame.com>
# Contributor: Bhushan Shah <bhush94 at gmail dot com>
# Contributor: Devin Lin <devin@kde.org>
_mode=host
pkgname=plasma-dialer
pkgdesc="Phone Dialer for Plasma Mobile"
pkgver=24.08.0.r0.g87e321b
pkgrel=1
_arches=all
arch=(
    x86_64
    armv7h
    aarch64
)
license=(GPL-3.0-or-later)
depends=(
    desktop-file-utils
    kcontacts
    modemmanager-qt
    kpeople
    libphonenumber
    kdbusaddons
    kirigami-addons
    knotifications
    ki18n
    kio
    qt6-declarative
    qtmpris
    callaudiod
)
makedepends=(
    cmake
    extra-cmake-modules
    kdoctools
    plasma-wayland-protocols
)
_commit=87e321b7e347346fbce4ac92c4c99539079f92cb
source=(
    "git+https://invent.kde.org/plasma-mobile/plasma-dialer.git#commit=$_commit"
)
sha256sums=(
    SKIP
)

pkgver() {
    cd "$pkgname"
    git describe --long --abbrev=7 --tags "$_commit" | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake ../$pkgname \
        -DCMAKE_INSTALL_LIBEXECDIR=lib \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DLIB_INSTALL_DIR=lib \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
        -DCMAKE_BUILD_TYPE=Release
    make
}

check() {
    cd build
    ctest --output-on-failure
}

package() {
    cd build
    make DESTDIR="$pkgdir/" install
}
