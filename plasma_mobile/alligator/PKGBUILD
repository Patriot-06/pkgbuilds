# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: Dan Johansen <strit@manjaro.org>
# Contributor: Devin Lin <devin@kde.org>
_mode=host
pkgname=alligator
pkgdesc="RSS Feed Reader"
pkgver=23.01.0.r0.gc2087cd
pkgrel=1
_arches=all
arch=(
    x86_64
    aarch64
    armv7h
)
license=(GPL-3.0-or-later)
depends=(
    kcoreaddons
    ki18n
    kconfig
    syndication
    kirigami-addons
)
makedepends=(
    cmake
    extra-cmake-modules
)
_commit=c2087cd576a9a24182e82c8d9b988ee0ea108d16
source=(
    "git+https://invent.kde.org/plasma-mobile/alligator.git#commit=$_commit"
)
sha256sums=(
    SKIP
)

pkgver() {
    cd "$pkgname"
    git describe --long --abbrev=7 --tags "$_commit" | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake ../$pkgname \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
        -DCMAKE_BUILD_TYPE=Release
    make
}

package() {
    cd build
    make DESTDIR="$pkgdir/" install
}

