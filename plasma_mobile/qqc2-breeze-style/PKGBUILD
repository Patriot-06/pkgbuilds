# Maintainer: Gustavo Castro < gustawho [ at ] gmail [ dot ] com >
# Contributor: Devin Lin <devin@kde.org>
_mode=host
pkgname=qqc2-breeze-style
pkgdesc="Breeze inspired QQC2 Style"
pkgver=5.27.9.r0.g752f863
pkgrel=1
_arches=all
arch=(
    x86_64
    aarch64
    armv7h
)
license=(GPL-3.0-or-later)
depends=(
    qt5-base
    qt5-declarative
    qt5-quickcontrols2
    kiconthemes5
    kirigami2
)
makedepends=(
    git
    extra-cmake-modules
    qt5-tools
)
_commit=752f8636203df7c358a14d5312f304d79437d869
source=(
    "git+https://invent.kde.org/plasma/qqc2-breeze-style.git#commit=$_commit"
)
sha256sums=(
    SKIP
)

pkgver() {
    cd "$pkgname"
    git describe --long --abbrev=7 --tags "$_commit" | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -B build -S "${pkgname}"
  cmake --build build --config Release
}

package() {
  DESTDIR="${pkgdir}" cmake --install build --config Release
}

