# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: Dan Johansen <strit@manjaro.org>
# Contributor: Devin Lin <devin@kde.org>
_mode=host
pkgname=tokodon
pkgdesc="Mastodon client for Plasma and Plasma Mobile"
pkgver=22.11.2.r0.gef2bbf2
pkgrel=1
_arches=all
arch=(
    x86_64
    armv7h
    aarch64
)
license=(GPL2)
depends=(
    qt5-base
    qt5-quickcontrols2
    qt5-multimedia
    qt5-svg
    qt5-websockets
    qtkeychain-qt5
    kio
    kirigami2
    kirigami-addons
    ki18n
    knotifications
    kconfig
    kcoreaddons
    kdbusaddons
    kitemmodels
    qqc2-desktop-style
)
makedepends=(
    cmake
    extra-cmake-modules
)
_commit=ef2bbf2bb879be80b73afa12b09d7e69c731a22e
source=(
    "git+https://invent.kde.org/network/tokodon.git#commit=$_commit"
)
sha256sums=(
    SKIP
)

pkgver() {
    cd "$pkgname"
    git describe --long --abbrev=7 --tags "$_commit" | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake "$srcdir/tokodon" \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DBUILD_TESTING=OFF \
        -DLIB_INSTALL_DIR=lib \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
        -DCMAKE_BUILD_TYPE=Release
    make
}

package() {
    cd build
    make DESTDIR="$pkgdir/" install
}
