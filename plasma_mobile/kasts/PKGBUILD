# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: Dan Johansen <strit@manjaro.org>
# Contributor: Bernhard Landauer <bernhard@manjaro.org>
# Contributor: Furkan K. <furkan@fkardame.com>
# Contributor: Bhushan Shah <bhush94 at gmail dot com>
# Contributor: Devin Lin <devin@kde.org>
_mode=host
pkgname=kasts
pkgdesc="Kirigami-based podcast player"
pkgver=23.01.0.r0.gb910725
pkgrel=1
_arches=all
arch=(
    x86_64
    aarch64
    armv7h
)
license=(GPL-2.0-or-later)
depends=(
    ki18n
    kconfig
    kcoreaddons
    kirigami2
    purpose
    syndication
    qt5-multimedia
    qt5-quickcontrols2
    qtkeychain-qt5
    taglib
    syndication
    threadweaver
    kirigami-addons
    gst-plugins-good
)
makedepends=(
    cmake
    extra-cmake-modules
    qt5-tools
    qt5-svg
)
_commit=b910725082615506ea530c08d4c8c0d54beadad0
source=(
    "git+https://invent.kde.org/plasma-mobile/kasts.git#commit=$_commit"
)
sha256sums=(
    SKIP
)

pkgver() {
    cd "$pkgname"
    git describe --long --abbrev=7 --tags "$_commit" | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir/" install
} 
