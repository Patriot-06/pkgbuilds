# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: Bernhard Landauer <bernhard@manjaro.org>
# Contributor: Devin Lin <devin@kde.org>
_mode=host
pkgname=krecorder
pkgdesc="Audio recording app for Plasma."
pkgver=23.01.0.r0.g0e5bec8
pkgrel=1
_arches=all
arch=(
    x86_64
    aarch64
    armv7h
)
license=(GPL-3.0-or-later)
depends=(
    ki18n
    kconfig
    kirigami2
    qt5-multimedia
    qt5-svg
    gst-plugins-good
    plasma-framework
    kirigami-addons
)
makedepends=(
    qt5-tools
    extra-cmake-modules
)
_commit=0e5bec80960f74a4ac75803522d55e84cae9b9d9
source=(
    "git+https://invent.kde.org/plasma-mobile/krecorder.git#commit=$_commit"
)
sha256sums=(
    SKIP
)

pkgver() {
    cd "$pkgname"
    git describe --long --abbrev=7 --tags "$_commit" | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    mkdir build
}

build() {
    cd build
    cmake ../$pkgname \
        -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr
    make
}

package() {
    cd build
    make DESTDIR="$pkgdir" install
}
