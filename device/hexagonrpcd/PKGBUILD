# Maintainer: Raihan Ahamed (raihan2000) <raihan1999ahamed@gmail.com>
_mode=host
_crossdirect=false
pkgname=(hexagonrpcd)
pkgdesc="Qualcomm HexagonFS daemon"
pkgver=0.3.2.r0.gb7eff23
pkgrel=1
_arches=all
arch=(
    x86_64
    armv7h
    aarch64
)
license=(GPL-3.0-or-later)
url="https://gitlab.com/sdm670-mainline/hexagonrpc"
depends=(glibc)
makedepends=(
    linux-headers
    meson
    git
)
_commit=b7eff23db1b11541d11175bd3f42bd4c85214baf  # tags/0.3.2^0
_fastrpc_commit=1a78a5e54a75019e5d02e96f19ec0575212398b4
source=(
    "git+https://gitlab.com/sdm670-mainline/hexagonrpc.git#commit=$_commit"
    "https://gitlab.alpinelinux.org/alpine/aports/-/raw/${_fastrpc_commit}/community/hexagonrpcd/10-fastrpc.rules"
    hexagonrpcd-adsp-rootpd.service
    hexagonrpcd-adsp-sensorspd.service
    hexagonrpcd-sdsp.service
)
sha256sums=(
    58ad8305f9cc06885dd2e589ed8637b1b8428f6930c7f36001b6b4570cfabd81
    41dfc4e8c4fd88f461a5a6e4a4e86849eb302f8bcb2b5ce2efa9690f5415d52d
    c89cb83fc7fecb232f530bcd60a3dd2d6846426f7c90173a1e078c6e15dc0bc0
    c69215694bedbc366fb9aebc8c6c08f27bb612753626f1083b4d30fcefd2fa04
    132aff498f306dd1951ae00aaae20b3b8df57d0c6bc92cb579de0e127cac73c4
)

pkgver() {
    cd hexagonrpc
    git describe --long --abbrev=7 --tags $_commit | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    arch-meson hexagonrpc build
    meson compile -C build
}

package() {
    DESTDIR="$pkgdir" meson install --no-rebuild -C build

    echo -e 'g fastrpc\nu fastrpc - "Qualcomm HexagonFS service" /var/lib/fastrpc' |
      install -Dm644 /dev/stdin "$pkgdir/usr/lib/sysusers.d/fastrpc.conf"

    # Allow access for FastRPC node for FastRPC user/group
    install -Dm 644 "$srcdir"/10-fastrpc.rules -t "$pkgdir"/usr/lib/udev/rules.d/

    install -Dm755 "$srcdir"/$pkgname-adsp-rootpd.service "$pkgdir"/usr/lib/systemd/system/$pkgname-adsp-rootpd.service
    install -Dm755 "$srcdir"/$pkgname-adsp-sensorspd.service "$pkgdir"/usr/lib/systemd/system/$pkgname-adsp-sensorspd.service
    install -Dm755 "$srcdir"/$pkgname-sdsp.service "$pkgdir"/usr/lib/systemd/system/$pkgname-sdsp.service
}
