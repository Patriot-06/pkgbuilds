_mode=cross
_nodeps=true
pkgname=device-msm8953-common
pkgdesc="Common package for Qualcomm MSM8953 devices"
pkgver=0.5
pkgrel=1
_arches=specific
arch=(aarch64)
license=(MIT)
provides=(alsa-ucm-conf)
conflicts=(alsa-ucm-conf)
depends=(
    linux-firmware-qcom
    linux-msm8953
    boot-lk2nd-msm8953-git
    qrtr-git
    rmtfs-git
    boot-android-common
    meta-modem-qcom
    q6voiced
)
_ucm_commit=01b9629916bd51e47481b97a29a0078bb6a72aec
_pmos_commit=fa6150b289438cb26c98915a39f569255b391303
source=(
    msm8953-common.conf
    "https://gitlab.com/postmarketOS/pmaports/-/raw/${_pmos_commit}/device/community/soc-qcom-msm8953/90-feedbackd-pm8xxx-vib.rules"
    "https://gitlab.com/postmarketOS/pmaports/-/raw/${_pmos_commit}/device/community/soc-qcom-msm8953/q6voiced.conf"
    "git+https://github.com/msm8953-mainline/alsa-ucm-conf.git/#commit=$_ucm_commit"
    51-custom.lua
)
sha256sums=(
    93624e7a003659b9af9cc4bf2c8f3b027ff3cb74af11f820ade88ef53415cab6
    5ad4650f48fab20e4d93dfde00687e10048a0e426a745ede313168f02e31367c
    21b8395e7729c785c0446be5d88e52873b0fa46812111b91bcd5c3af7dde24da
    SKIP
    5e19f243756046ef88e33155f398dcaf637bf67914d18122040ce7ae604c284f
)

package() {
    install -Dm644 "$srcdir"/msm8953-common.conf "$pkgdir"/etc/kupfer/mkinitcpio.conf.d/msm8953-common.conf

    # taken from pmos:
    install -Dm644 "$srcdir"/90-feedbackd-pm8xxx-vib.rules "$pkgdir"/usr/lib/udev/rules.d/90-feedbackd-pm8xxx-vib.rules
    install -Dm644 "$srcdir"/q6voiced.conf "$pkgdir"/etc/conf.d/q6voiced

    # alsa ucm config
    cd "$srcdir"/alsa-ucm-conf
    export alsadir="${pkgdir}/usr/share/alsa/"
    find ucm2 -type f -iname "*.conf" -exec install -vDm 644 {} "$alsadir"{} \;
    find ucm2 -type l -iname "*.conf" -exec bash -c 'install -vdm 755 "${alsadir}$(dirname "{}")" && cp -dv "{}" "${alsadir}{}"' \;
    install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
    install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
    install -vDm 644 ucm2/README.md -t "$pkgdir/usr/share/doc/$pkgname/ucm2"

    # pipewire low volume and crackling fix
    # taken from here: https://gitlab.freedesktop.org/pipewire/pipewire/-/issues/3193
    install -Dm644 "$srcdir"/51-custom.lua "$pkgdir"/usr/share/wireplumber/main.lua.d/51-custom.lua

}
