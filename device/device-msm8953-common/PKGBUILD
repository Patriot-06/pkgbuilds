_mode=cross
_nodeps=true
pkgname=device-msm8953-common
pkgdesc="Common package for Qualcomm MSM8953 devices"
pkgver=0.5
pkgrel=8
_arches=specific
arch=(aarch64)
license=(MIT)
provides=(alsa-ucm-conf)
conflicts=(alsa-ucm-conf)
depends=(
    linux-firmware-qcom
    linux-msm8953
    boot-lk2nd-msm8953-git
    qrtr-git
    rmtfs-git
    boot-android-common
    meta-modem-qcom
    q6voiced
)
_ucm_commit=b6860eae577ddea6f55834850fb4ed403f208d2f
_pmos_commit=dfee3e43658f45720f92a922255a41040ce99821
source=(
    msm8953-common.conf
    "https://gitlab.com/postmarketOS/pmaports/-/raw/${_pmos_commit}/device/community/soc-qcom-msm8953/90-feedbackd-pm8xxx-vib.rules"
    "https://gitlab.com/postmarketOS/pmaports/-/raw/${_pmos_commit}/device/community/soc-qcom-msm8953/q6voiced.conf"
    "git+https://github.com/msm8953-mainline/alsa-ucm-conf.git/#commit=$_ucm_commit"
    51-custom.conf
    adreno-a506-quirks.sh
)
sha256sums=(
    98201c2c49a49b2b00978b09de7215021187b7b18731be7552258fb00f3c43bf
    5ad4650f48fab20e4d93dfde00687e10048a0e426a745ede313168f02e31367c
    21b8395e7729c785c0446be5d88e52873b0fa46812111b91bcd5c3af7dde24da
    SKIP
    0d1675f4ff950591a5a51b21280edbd86c820783c8609833dbde86f9769014ce
    ae2dc1656891a9b50e43c1f3fc3d1103bc929a38b70dcc8305238aba38a487ca
)

package() {
    install -Dm644 "$srcdir"/msm8953-common.conf "$pkgdir"/etc/kupfer/mkinitcpio.conf.d/msm8953-common.conf

    # taken from pmos:
    install -Dm644 "$srcdir"/90-feedbackd-pm8xxx-vib.rules "$pkgdir"/usr/lib/udev/rules.d/90-feedbackd-pm8xxx-vib.rules
    install -Dm644 "$srcdir"/q6voiced.conf "$pkgdir"/etc/conf.d/q6voiced
    install -Dm644 "$srcdir"/adreno-a506-quirks.sh "$pkgdir"/etc/profile.d/adreno-a506-quirks.sh

    # alsa ucm config
    cd "$srcdir"/alsa-ucm-conf
    export alsadir="${pkgdir}/usr/share/alsa/"
    find ucm2 -type f -iname "*.conf" -exec install -vDm 644 {} "$alsadir"{} \;
    find ucm2 -type l -iname "*.conf" -exec bash -c 'install -vdm 755 "${alsadir}$(dirname "{}")" && cp -dv "{}" "${alsadir}{}"' \;
    install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
    install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
    install -vDm 644 ucm2/README.md -t "$pkgdir/usr/share/doc/$pkgname/ucm2"

    # pipewire low volume and crackling fix
    # taken from here: https://gitlab.freedesktop.org/pipewire/pipewire/-/issues/3193
    # upd 04.2024: since wireplumber 0.5 it should be formatted in SPA-JSON format and placed at neighbor directory

    # before wirepulmber 0.5
    # install -Dm644 "$srcdir"/51-custom.lua "$pkgdir"/usr/share/wireplumber/main.lua.d/51-custom.lua

    # wireplumber 0.5
    install -Dm644 "$srcdir"/51-custom.conf "$pkgdir"/usr/share/wireplumber/wireplumber.conf.d/51-custom.conf

    # Could not activate connection: not authorized to control networking
    mkdir -p "$pkgdir"/etc/NetworkManager/conf.d
    cat >>"$pkgdir"/etc/NetworkManager/conf.d/any-user.conf <<EOF

[main]
auth-polkit=false
EOF
}
