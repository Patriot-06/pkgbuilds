_mode=cross
_nodeps=true
pkgname=device-msm8953-xiaomi-mido
pkgver=0.5
pkgrel=2
_arches=specific
arch=(aarch64)
license=(MIT)
makedepends=(devicepkg-helpers)
_commit=fa6150b289438cb26c98915a39f569255b391303
depends=(
    device-msm8953-common
    firmware-msm8953-xiaomi-mido
    msm-firmware-loader
    qbootctl
    q6voiced
)
source=(
    "https://gitlab.com/postmarketOS/pmaports/-/raw/${_commit}/device/community/device-xiaomi-mido/deviceinfo"
    "https://gitlab.com/postmarketOS/pmaports/-/raw/${_commit}/device/community/device-xiaomi-mido/modules-initfs"
    xiaomi-mido.conf
    machine-info
)
sha256sums=(
    SKIP
    SKIP
    SKIP
    SKIP
)

package() {
    cp "$srcdir"/deviceinfo "$srcdir"/deviceinfo_
    cat >>"$srcdir"/deviceinfo_ <<EOF

deviceinfo_partitions_data="/dev/mmcblk0p49"
deviceinfo_partitions_microsd="/dev/mmcblk1"

deviceinfo_lk2nd="true"
EOF

# compability with kupfer: init scripts expect the list of modules to be a variable in the deviceinfo file
    cat >>"$srcdir"/deviceinfo_ <<EOF
    
deviceinfo_modules_initfs="$(cat "$srcdir"/modules-initfs | tr '\n' ' ')"
EOF
    install -Dm644 "$srcdir"/deviceinfo_ "$pkgdir"/etc/kupfer/deviceinfo

    install -Dm644 "$srcdir"/xiaomi-mido.conf "$pkgdir"/etc/kupfer/mkinitcpio.conf.d/xiaomi-mido.conf
    install -Dm644 "$srcdir"/machine-info "$pkgdir"/etc/machine-info
}
