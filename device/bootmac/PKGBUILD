_mode=cross
_nodeps=true
pkgname=bootmac
pkgdesc="Configure MAC addresses at boot"
pkgver=0.3.0.r0.gef147ec
pkgrel=1
_arches=specific
arch=(
    aarch64
)
license=(GPL-3.0-or-later)
url=https://gitlab.com/postmarketOS/bootmac
depends=(bluez-utils)
_commit=ef147ec9973ac473275e4259125255e3a6f52edb
source=(
    "git+https://gitlab.com/postmarketOS/bootmac.git#commit=$_commit"
    bootmac-bt.service
)
sha256sums=(
    SKIP
    5c9b2a4539b4d8a351c3545d75db1a6437bc1ebda0d29e57de9ffe460180e60a
)

pkgver() {
    cd bootmac
    git describe --long --tags --abbrev=7 "$_commit" | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

package() {
    cd bootmac

    install -Dm644 bootmac.rules "$pkgdir"/usr/lib/udev/rules.d/90-bootmac.rules
    install -Dm755 bootmac "$pkgdir"/usr/bin/bootmac

    install -Dm644 "$srcdir"/bootmac-bt.service "$pkgdir"/usr/lib/systemd/system/bootmac-bt.service
}
