_mode=cross
pkgname=reboot-mode
pkgdesc="Reboot the device to a specific mode"
pkgver=1.0.0.r0.gae8b37b
pkgrel=1
_arches=all
arch=(
    aarch64
    x86_64
)
license=(GPL3)
url=https://gitlab.com/postmarketOS/reboot-mode
makedepends=(linux-headers)
_commit=ae8b37b33eaa3062787e9b605b5d4d3fbdf19aea
source=("git+$url#commit=$_commit")
sha256sums=(SKIP)

pkgver() {
    cd "$pkgname"
    git describe --long --abbrev=7 "$_commit" --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    cd "$pkgname"
    export CFLAGS="$CFLAGS -static"
    # shellcheck disable=SC2086
    ${CC:-gcc} $CFLAGS reboot-mode.c -o reboot-mode.o -c
    # shellcheck disable=SC2086
    ${CC:-gcc} $CFLAGS reboot-mode.o -o reboot-mode
}

package() {
    install -Dm755 "$srcdir"/"$pkgname"/reboot-mode \
        "$pkgdir"/usr/bin/reboot-mode
}
