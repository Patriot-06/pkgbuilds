_mode=host
_pkgname=rmtfs
pkgname="$_pkgname-git"
pkgdesc="Qualcomm Remote Filesystem Service Implementation"
pkgver=r58.695d066
pkgrel=1
_arches=specific
arch=(aarch64)
license=(BSD-3-Clause)
url="https://github.com/andersson/$_pkgname"
provides=("$_pkgname")
depends=(
    qrtr-git
    udev
)
_commit=695d0668ffa6e2a4bf6e676f3c58a444a5d67690
source=(
    "git+$url#commit=$_commit"
    udev.rules
)
sha256sums=(
    SKIP
    0c2f26d40d9d18e3089c6d836b64231c4f7e0c1ca41686fe7c9d1eb495dc6bbe
)

pkgver() {
    cd "$_pkgname"

    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

build() {
    cd "$_pkgname"

    make prefix=/usr
}

package() {
    cd "$_pkgname"

    make prefix=/usr DESTDIR="$pkgdir/" install
    install -Dm644 "$srcdir"/udev.rules "$pkgdir/usr/lib/udev/rules.d/65-$_pkgname.rules"
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$_pkgname/COPYING
}
