_mode=cross
_nodeps=true
pkgname=device-msm8953-xiaomi-tissot
pkgdesc="Xiaomi Mi A1"
pkgver=0.6
pkgrel=2
_arches=specific
arch=(aarch64)
license=(MIT)
makedepends=(devicepkg-helpers)
_commit=fa6150b289438cb26c98915a39f569255b391303
depends=(
    device-msm8953-common
    firmware-msm8953-xiaomi-tissot
    msm-firmware-loader
    qbootctl
    q6voiced
)
source=(
    "https://gitlab.com/postmarketOS/pmaports/-/raw/${_commit}/device/community/device-xiaomi-tissot/deviceinfo"
    "https://gitlab.com/postmarketOS/pmaports/-/raw/${_commit}/device/community/device-xiaomi-tissot/modules-initfs"
    xiaomi-tissot.conf
    machine-info
)
sha256sums=(
    08c2fe2b79129e5557b28ef42a8ad4e484b1febf8a79af447a9178155639ef0e
    dcb8a11719721fe36ca14ac96ee8925cd8cdaba48d1dd357d8e6ddcd3ef0b79e
    e3b0c44298fc1c149afbf4c8996fb92427ae41e4649b934ca495991b7852b855
    7729d6a89258b470cadd48cfbc704bab7791501c53f45f50737dea6b441a3171
)

package() {
    cp "$srcdir"/deviceinfo "$srcdir"/deviceinfo_
    cat >>"$srcdir"/deviceinfo_ <<EOF

deviceinfo_partitions_data="/dev/mmcblk0p49"
deviceinfo_partitions_microsd="/dev/mmcblk1"

deviceinfo_lk2nd="true"
EOF

# compability with kupfer: init scripts expect the list of modules to be a variable in the deviceinfo file
    cat >>"$srcdir"/deviceinfo_ <<EOF
    
deviceinfo_modules_initfs="$(cat "$srcdir"/modules-initfs | tr '\n' ' ')"
EOF
    install -Dm644 "$srcdir"/deviceinfo_ "$pkgdir"/etc/kupfer/deviceinfo

    install -Dm644 "$srcdir"/xiaomi-tissot.conf "$pkgdir"/etc/kupfer/mkinitcpio.conf.d/xiaomi-tissot.conf
    install -Dm644 "$srcdir"/machine-info "$pkgdir"/etc/machine-info
}
