_mode=cross
_nodeps=true
pkgname=device-sdm845-common
pkgdesc="Common package for Qualcomm SDM845 devices"
pkgver=0.5
pkgrel=5
_arches=specific
arch=(aarch64)
license=(MIT)
provides=(alsa-ucm-conf)
conflicts=(alsa-ucm-conf)
depends=(
    linux-firmware-qcom
    linux-firmware-ath10k
    linux-sdm845
    pd-mapper-git
    qrtr-git
    rmtfs-git
    tqftpserv-git
    boot-android-common
    meta-modem-qcom
    q6voiced
    bootmac
)
_ucm_commit=aaa7889f7a6de640b4d78300e118457335ad16c0
_pmos_commit=801283d9f914a895ab2b331102788fc7261994a2
source=(
    sdm845-common.conf
    sdm845.lst
    sdm845_callaudio.lst
    bootmac-bt.lst
    callaudio-idle-workaround.service
    alsa-restore.service.override
    "https://gitlab.com/postmarketOS/pmaports/-/raw/${_pmos_commit}/device/community/soc-qcom-sdm845/call_audio_idle_suspend_workaround.sh"
    "https://gitlab.com/postmarketOS/pmaports/-/raw/${_pmos_commit}/device/community/soc-qcom-sdm845/90-feedbackd-pmi8998.rules"
    "https://gitlab.com/postmarketOS/pmaports/-/raw/${_pmos_commit}/device/community/soc-qcom-sdm845/51-qcom-sdm845.conf"
    "git+https://gitlab.com/sdm845-mainline/alsa-ucm-conf.git/#commit=$_ucm_commit"
)
sha256sums=(
    557bde976b39d767305b870f0de1f5572bda953afc7d8ea153bf4b6cd2ae913d
    b6834f1eb597cf5846ce730f52d3b24b4b3eaca66388d2b8611d1ca5e37dbdf5
    1ae2de281a337ba5c135368f3a68e414cd459affe04432706338cda7a84f1044
    05bfc075fa627d4cee84275e81100319259f0c414727c868909d3f685ef1eaf3
    2df59440faccce3ce1ff8a2666c08252d2519e7ff9ab9b66c0d7cac9fcc2a5af
    6a86566df76ba9dd6651353c00a9cdb2c253bbd88b580a6c8acb04b3f97f8c87
    7fb4dc39965f348dd434f49cb0ff7b7df5a873f3586541c090ffe76e1a1703d9
    897f2f5533644668b63e5fbff917e050af1cfb7472553c3ee520388c932ab6f9
    10cd88f61c136272b915ea41e11048e6df2c20543fbaa37902e4f019ea738c1d
    SKIP
)

package() {
    install -Dm644 "$srcdir"/sdm845-common.conf "$pkgdir"/etc/kupfer/mkinitcpio.conf.d/sdm845-common.conf
    install -Dm644 "$srcdir"/sdm845.lst "$pkgdir"/etc/kupfer/systemd/sdm845.lst
    install -Dm644 "$srcdir"/bootmac-bt.lst "$pkgdir"/etc/kupfer/systemd/bootmac-bt.lst

    install -Dm644 "$srcdir"/callaudio-idle-workaround.service "$pkgdir"/usr/lib/systemd/user/callaudio-idle-workaround.service
    install -Dm644 "$srcdir"/sdm845_callaudio.lst "$pkgdir"/etc/kupfer/systemd/user/sdm845_callaudio.lst

    # temporary alsa-restore workaround for broken audio on sdm845
    install -Dm644 "$srcdir"/alsa-restore.service.override "$pkgdir"/usr/lib/systemd/system/alsa-restore.service.d/sdm845-fix.conf

    # taken from pmos:
    install -Dm755 "$srcdir"/call_audio_idle_suspend_workaround.sh "$pkgdir"/usr/bin/callaudio-idle-workaround
    install -Dm644 "$srcdir"/90-feedbackd-pmi8998.rules "$pkgdir"/usr/lib/udev/rules.d/90-feedbackd-pmi8998.rules
    install -Dm644 "$srcdir"/51-qcom-sdm845.conf "$pkgdir"/usr/share/wireplumber/wireplumber.conf.d/51-qcom-sdm845.conf

    # alsa ucm config
    cd "$srcdir"/alsa-ucm-conf
    export alsadir="${pkgdir}/usr/share/alsa/"
    find ucm2 -type f -iname "*.conf" -exec install -vDm 644 {} "$alsadir"{} \;
    find ucm2 -type l -iname "*.conf" -exec bash -c 'install -vdm 755 "${alsadir}$(dirname "{}")" && cp -dv "{}" "${alsadir}{}"' \;
    install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
    install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
    install -vDm 644 ucm2/README.md -t "$pkgdir/usr/share/doc/$pkgname/ucm2"
}
