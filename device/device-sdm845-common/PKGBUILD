_mode=cross
pkgname=device-sdm845-common
pkgdesc="Common package for Qualcomm SDM845 devices"
pkgver=0.3
pkgrel=1
_arches=specific
arch=(aarch64)
license=(MIT)
provides=(alsa-ucm-conf)
conflicts=(alsa-ucm-conf)
depends=(
    linux-firmware-qcom
    linux-firmware-ath10k
    linux-sdm845
    pd-mapper-git
    qrtr-git
    rmtfs-git
    tqftpserv-git
    boot-android-common
)
install=sdm845-common.install
_ucm_commit=621c71fd5f5742c60d38766ebb2d1bd3b863a2a4
source=(
    sdm845-common.conf
    "git+https://gitlab.com/sdm845-mainline/alsa-ucm-conf.git/#commit=$_ucm_commit"
)
sha256sums=(
    557bde976b39d767305b870f0de1f5572bda953afc7d8ea153bf4b6cd2ae913d
    SKIP
)

package() {
    install -Dm644 "$srcdir"/sdm845-common.conf "$pkgdir"/etc/kupfer/mkinitcpio.conf.d/sdm845-common.conf

    cd "$srcdir"/alsa-ucm-conf
    find ucm2 -type f -iname "*.conf" -exec install -vDm 644 {} "${pkgdir}/usr/share/alsa/"{} \;
    find ucm2 -type l -iname "*.conf" -exec cp -dv {} "${pkgdir}/usr/share/alsa/"{} \;
    install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
    install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
    install -vDm 644 ucm2/README.md -t "$pkgdir/usr/share/doc/$pkgname/ucm2"
}
