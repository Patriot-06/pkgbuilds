_mode=host
pkgname=callaudiod
pkgdesc="Call audio routing daemon"
pkgver=0.1.3.r0.g4cb8bd9
pkgrel=1
_arches=all
arch=(
    x86_64
    aarch64
)
license=(GPL3)
url=https://gitlab.com/mobian1/callaudiod
depends=(
    alsa-lib
    glib2
    libpulse
)
makedepends=(meson)
_commit=4cb8bd9d228d13ea67e79f4c37c674f412b32d20
source=("git+$url#commit=$_commit")
sha256sums=(SKIP)

pkgver() {
    cd "$srcdir/$pkgname"
    git describe --long "$_commit" | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    cd $pkgname
    arch-meson . output
    ninja -C output
}

package() {
    cd $pkgname
    DESTDIR="$pkgdir" ninja -C output install
}
