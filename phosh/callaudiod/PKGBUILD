_mode=host
pkgname=callaudiod
pkgdesc="Call audio routing daemon"
pkgver=0.1.7.r0.g92cf20a
pkgrel=1
_arches=all
arch=(
    x86_64
    aarch64
)
license=(GPL3)
url=https://gitlab.com/mobian1/callaudiod
depends=(
    alsa-lib
    glib2
    libpulse
)
makedepends=(meson)
_commit=92cf20a93903fe3d7969f4002643b011011243f7
source=("git+$url#commit=$_commit")
sha256sums=(SKIP)

pkgver() {
    cd "$srcdir/$pkgname"
    git describe --long --tags --abbrev=7 "$_commit" | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    cd $pkgname
    arch-meson . output
    ninja -C output
}

package() {
    cd $pkgname
    DESTDIR="$pkgdir" ninja -C output install
}
