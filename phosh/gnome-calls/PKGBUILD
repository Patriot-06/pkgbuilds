# Maintainer: Danct12 <danct12@disroot.org>
_mode=host
pkgname=gnome-calls
pkgdesc="Make and receive PSTN phone calls"
pkgver=44.2.r0.g91e5cad
pkgrel=1
_arches=all
arch=(
    x86_64
    armv7h
    aarch64
)
license=(GPL)
url="https://gitlab.gnome.org/GNOME/calls"
replaces=(calls)
depends=(
    evolution-data-server
    feedbackd folks gobject-introspection
    gom
    gtk3
    libhandy
    libpeas
    modemmanager
    callaudiod
    sofia-sip
    wayland-protocols
)
makedepends=(
    meson
    ninja
    vala
    git
    python-docutils
)
_commit="91e5cad939f6753bc232c8990be295b068473949"
source=("git+https://gitlab.gnome.org/GNOME/calls.git#commit=$_commit")
sha256sums=(SKIP)

pkgver() {
    cd calls
    git describe --long --abbrev=7 --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd calls
  git submodule--helper update --init

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  arch-meson calls output
  ninja -C output
}

package() {
  DESTDIR="$pkgdir" ninja -C output install
}
