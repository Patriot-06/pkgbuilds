# Maintainer: Raihan Ahamed <raihan1999ahamed@gmail.com>
# Original Author: Danct12 <danct12@disroot.org>
_mode=host
pkgname=purple-telegram
pkgdesc="Purple Telegram plugin"
pkgver=1.4.7.r0.g7d1d61b
pkgrel=1
_arches=all
arch=(
    x86_64
    armv7h
    aarch64
)
license=(GPL)
url="https://github.com/majn/telegram-purple"
depends=(
    glib2
    libpurple
    libwebp
)
makedepends=(git)
_commit="7d1d61be591d181295e470b38f508867bbf0efa9" # tags/v1.4.7
source=($pkgname::git+https://github.com/majn/telegram-purple.git#commit=$_commit)
sha256sums=(SKIP)

pkgver() {
    cd "$pkgname"
    git describe --long --abbrev=7 --tags $_commit | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd "$pkgname"
    git submodule update --init --recursive
}

build() {
    cd "$pkgname"
    ./configure
    make
}

package() {
    cd "$pkgname"
    DESTDIR="$pkgdir" make install
}
