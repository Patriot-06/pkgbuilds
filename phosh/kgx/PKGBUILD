_mode=host
pkgname=kgx
pkgdesc="A simple user-friendly terminal emulator for the GNOME desktop"
pkgver=43.0.r0.gcffddb1
pkgrel=1
_arches=all
arch=(
    x86_64
    aarch64
)
license=(GPL)
url=https://gitlab.gnome.org/GNOME/console
depends=(
    vte4
    gtk4
    glib2
    libadwaita
    libgtop
    libnautilus-extension
)
makedepends=(
    meson
    sassc
)
_commit=cffddb1a7bbf8ad1b1616cc026909411e218db1a
source=("git+$url#commit=$_commit")
sha256sums=(SKIP)

pkgver() {
    cd console
    git describe --long "$_commit" | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    arch-meson console output
    ninja -C output || true # This is issue with the generation: fatal error: kgx-enums.h: No such file or directory
    ninja -C output
}

package() {
    DESTDIR="$pkgdir" ninja -C output install
}
