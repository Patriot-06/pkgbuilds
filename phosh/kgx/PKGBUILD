_mode=host
pkgname=kgx
pkgdesc="A simple user-friendly terminal emulator for the GNOME desktop"
pkgver=0.4.0.r0.g67e112c
pkgrel=1
_arches=all
arch=(
    x86_64
    aarch64
)
license=(GPL)
url=https://gitlab.gnome.org/GNOME/console
depends=(
    vte3
    gtk3
    glib2
    libhandy
    libgtop
    libnautilus-extension
)
makedepends=(
    meson
    sassc
)
_commit=67e112c5d7cb406153da8e7bf6f789d56e91e3bf
source=("git+$url#commit=$_commit")
sha256sums=(SKIP)

pkgver() {
    cd console
    git describe --long "$_commit" | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    arch-meson console output -Dgeneric=true -Dgtk_doc=false
    ninja -C output || true # This is issue with the generation: fatal error: kgx-enums.h: No such file or directory
    ninja -C output
}

package() {
    DESTDIR="$pkgdir" ninja -C output install
}
