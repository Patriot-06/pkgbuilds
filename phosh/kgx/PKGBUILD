_mode=host
pkgname=kgx
pkgdesc="A minimal terminal for GNOME"
pkgver=r380.67e112c
pkgrel=1
_arches=all
arch=(
    x86_64
    aarch64
)
license=(GPL)
url=https://gitlab.gnome.org/ZanderBrown/kgx
depends=(
    vte3
    gtk3
    glib2
    libhandy
    libgtop
    libnautilus-extension
)
makedepends=(
    meson
    sassc
)
_commit=67e112c5d7cb406153da8e7bf6f789d56e91e3bf
source=("git+$url#commit=$_commit")
sha256sums=(SKIP)

pkgver() {
    cd "$pkgname"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    arch-meson $pkgname output -Dgeneric=true -Dgtk_doc=false
    ninja -C output || true # This is issue with the generation: fatal error: kgx-enums.h: No such file or directory
    ninja -C output
}

package() {
    DESTDIR="$pkgdir" ninja -C output install
}
