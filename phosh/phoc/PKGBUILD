_mode=host
pkgname=phoc
pkgdesc="A compositor for phones based on wlroots"
pkgver=0.13.1.r0.g4f72cf1
pkgrel=1
_arches=all
arch=(
    x86_64
    aarch64
)
license=(GPL)
url=https://gitlab.gnome.org/World/Phosh/phoc
depends=(
    gobject-introspection
    gnome-desktop
    libinput
    mutter
    wayland-protocols
    seatd
    xcb-util-wm
)
makedepends=(
    meson
    ninja
)

_commit=4f72cf1e67a3f0f224326b2662b7dedab3f5a8cf
_wlroots_commit=5016f79f2734c1b1d4d1f1f33f0374c3043f2ea0
source=(
    "git+$url#commit=$_commit"
    "git+https://source.puri.sm/Librem5/wlroots#commit=$_wlroots_commit"
)
sha256sums=(
    SKIP
    SKIP
)

pkgver() {
    cd "$pkgname"
    git describe --long "$_commit" | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd "$pkgname"
    git submodule init
    git submodule set-url subprojects/wlroots "${srcdir}/wlroots"
    git submodule update
}

build() {
    arch-meson ${pkgname} output -Dtests=false \
        -Dembed-wlroots=enabled --default-library=static \
        -Dwlroots:examples=false \
        -Dwlroots:x11-backend=disabled \
        -Dwlroots:xcb-errors=disabled
    ninja -C output
}

package() {
    DESTDIR="$pkgdir" ninja -C output install

    # Install scale-to-fit helper
    install -Dm755 ${pkgname}/helpers/scale-to-fit "$pkgdir"/usr/bin/scale-to-fit

    # Remove unnecessary files
    rm -r "$pkgdir"/usr/lib
    rm -r "$pkgdir"/usr/include
}
