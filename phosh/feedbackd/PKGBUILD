_mode=host
pkgname=feedbackd
pkgdesc="A daemon to provide haptic (and later more) feedback on events"
pkgver=0.0.0+git20211018.r0.g753fff3
pkgrel=1
_arches=all
arch=(
    x86_64
    aarch64
)
license=(GPL)
url=https://source.puri.sm/Librem5/feedbackd
depends=(
    gobject-introspection
    gsound
    json-glib
    libgudev
)
makedepends=(
    meson
    vala
)
_commit=753fff3e7ae1d0bce4f58cef721e45c50c404786
_fbdthemes_commit=5a145c5cb089f2189ce9ce2584516d5da2686040
source=(
    "git+$url#commit=$_commit"
    "git+https://source.puri.sm/Librem5/feedbackd-device-themes#commit=$_fbdthemes_commit"
)
sha256sums=(
    SKIP
    SKIP
)

pkgver() {
    cd "$pkgname"
    git describe --long "$_commit" | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    arch-meson ${pkgname} output -Dtests=false
    ninja -C output
}

package() {
    DESTDIR="$pkgdir" ninja -C output install
    install -Dm644 "$srcdir"/${pkgname}/debian/feedbackd.udev \
        "$pkgdir"/usr/lib/udev/rules.d/90-feedbackd.rules
    sed -i 's/libexec/lib/g' "$pkgdir"/usr/lib/udev/rules.d/90-feedbackd.rules

    # FIXME: We aren't supposed to abuse video group, but we need to find a
    #        efficient way to add user to feedbackd group.
    sed -i 's/-G feedbackd/-G video/g' "$pkgdir"/usr/lib/udev/rules.d/90-feedbackd.rules

    # It would make much more sense to bundle fbd device configuration with the pkg.
    find "${srcdir}/feedbackd-device-themes/data" -name \*.json \
        -exec cp {} "${pkgdir}/usr/share/feedbackd/themes" \;
}
