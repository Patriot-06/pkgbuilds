_mode=host
pkgname=phosh-mobile-settings
pkgdesc="A settings app for mobile specific things"
pkgver=0.29.0.r0.gafead55
pkgrel=1
_arches=all
arch=(
    x86_64
    aarch64
)
license=(GPL-3.0-or-later)
depends=(
    desktop-file-utils
    glib2
    gtk4
    libadwaita
    lm_sensors
    wayland-protocols
    phosh
)
makedepends=(
    meson
    ninja
)
_commit=afead556eae31cb239ed2f24a7ad1a2507a641be
source=(
    "git+https://gitlab.gnome.org/guidog/phosh-mobile-settings.git#commit=$_commit"
)
sha256sums=(
    SKIP
)

pkgver() {
    cd "$pkgname"
    git describe --long --tags --abbrev=7 "$_commit" | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    arch-meson ${pkgname} output
    ninja -C output
}

package() {
    DESTDIR="$pkgdir" meson install --no-rebuild -C output
}
