_mode=host
pkgname=phosh-mobile-settings
pkgdesc="A settings app for mobile specific things"
pkgver=0.22.0.r0.g810c3ab
pkgrel=1
_arches=all
arch=(
    x86_64
    aarch64
)
license=(GPL-3.0-or-later)
depends=(
    desktop-file-utils
    glib2
    gtk4
    libadwaita
    lm_sensors
    wayland-protocols
)
makedepends=(
    meson
    ninja
)
_commit=810c3abaac23210c88f9da8a9d3cd00a4768f730
source=(
    "git+https://gitlab.gnome.org/guidog/phosh-mobile-settings.git#commit=$_commit"
)
sha256sums=(
    SKIP
)

pkgver() {
    cd "$pkgname"
    git describe --long "$_commit" | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    arch-meson ${pkgname} output
    ninja -C output
}

package() {
    DESTDIR="$pkgdir" meson install --no-rebuild -C output
}
