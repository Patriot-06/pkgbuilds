# Maintainer: Raihan Ahamed <raihan1999ahamed@gmail.com>
# Co-Maintainer: Danct12 <danct12@disroot.org>
_mode=host
pkgname=purism-chatty
pkgdesc="SMS/MMS, Matrix and (optionally) XMPP messaging app"
pkgver=0.8.1.r0.gd02161d
pkgrel=1
_arches=all
arch=(
    x86_64
    armv7h
    aarch64
)
license=(GPL)
url=https://gitlab.gnome.org/World/Chatty
depends=(
    evolution-data-server
    feedbackd
    gnome-desktop-4
    gtk4
    libadwaita
    libmm-glib
    libolm
    libpurple
)
makedepends=(
    git
    itstool
    meson
    ninja
)
checkdepends=(
    xorg-server-xvfb
)
optdepends=(
    "libpurple-carbons: message carbons support"
    "libpurple-lurch: OMEMO encryption"
    "libpurple-xmpp-http-upload: XMPP HTTP File Upload support"
    "mmsd-tng: MMS support"
    "purple-telegram: Telegram chat protocol support"
)
_commit=d02161d47820070fe2911597f49fae4d0075aebd # tags/v0.8.1
source=("git+https://gitlab.gnome.org/World/Chatty.git#commit=${_commit}")
sha256sums=(SKIP)

pkgver() {
    cd Chatty
    git describe --long --tags --abbrev=7 "$_commit" | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd Chatty
    git submodule update --init
}

build() {
    arch-meson Chatty output
    ninja -C output
}

# FAIL: secure memory pool is not locked while in FIPS mode
#check() {
#    xvfb-run ninja -C output test
#}

package() {
    DESTDIR="$pkgdir" ninja -C output install
}
