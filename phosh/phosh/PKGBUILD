_mode=host
pkgname=phosh
pkgdesc="The phosh Shell"
pkgver=0.15.1.r609.g5411691
pkgrel=1
_arches=all
arch=(
    x86_64
    aarch64
)
license=(GPL)
url=https://gitlab.gnome.org/guidog/phosh
depends=(
    gtk3
    libhandy
    gnome-desktop
    gnome-session
    gnome-shell
    upower
    libpulse
    gcr
    feedbackd
    libnm
    phoc
    callaudiod
)
makedepends=(
    meson
    ninja
)
_commit=54116910d95a6e77a7653905531e313bda8fcc00
source=(
    "git+$url#commit=$_commit"
    pam_phosh
    sm.puri.OSK0.desktop
    phosh.service
)
sha256sums=(
    SKIP
    SKIP
    SKIP
    SKIP
)

pkgver() {
    cd "$pkgname"
    git describe --long "$_commit" | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g' | sed 's/^0.12/0.15/'
}

build() {
    arch-meson ${pkgname} output -Dtests=false -Dphoc_tests=disabled -Dsystemd=true
    ninja -C output
}

package() {
    DESTDIR="$pkgdir" ninja -C output install

    install -Dm755 output/tools/phosh-osk-stub \
         "$pkgdir"/usr/bin/phosh-osk-stub

    install -Dm644 "$srcdir"/phosh.service \
        "$pkgdir"/usr/lib/systemd/system/phosh.service
    install -Dm644 "$srcdir"/pam_phosh \
        "$pkgdir"/etc/pam.d/phosh
    install -Dm644 "$srcdir"/sm.puri.OSK0.desktop \
        "$pkgdir"/usr/share/applications/sm.puri.OSK0.desktop
}
