_mode=host
pkgname=phosh
pkgdesc="The phosh Shell"
pkgver=0.17.0.r0.ge9651c72
pkgrel=1
_arches=all
arch=(
    x86_64
    aarch64
)
license=(GPL)
url=https://gitlab.gnome.org/World/Phosh/phosh
depends=(
    gtk3
    libhandy
    gnome-desktop
    gnome-session
    gnome-shell
    upower
    libpulse
    gcr
    feedbackd
    libnm
    phoc
    callaudiod
)
makedepends=(
    meson
    ninja
)
_commit=e9651c72a6c3033597d40b135c230262a2bf4cb6
source=(
    "git+$url#commit=$_commit"
    pam_phosh
    phosh.service
)
sha256sums=(
    SKIP
    SKIP
    SKIP
)

pkgver() {
    cd "$pkgname"
    git describe --long "$_commit" | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    arch-meson ${pkgname} output -Dtests=false -Dphoc_tests=disabled -Dsystemd=true
    ninja -C output
}

package() {
    DESTDIR="$pkgdir" ninja -C output install

    install -Dm644 "$srcdir"/phosh.service \
        "$pkgdir"/usr/lib/systemd/system/phosh.service
    install -Dm644 "$srcdir"/pam_phosh \
        "$pkgdir"/etc/pam.d/phosh
}
