_mode=host
pkgname=phosh-osk-stub
pkgdesc="Stub for debugging OSK / input related issues with phosh"
pkgver=0.22.0.r0.gf4be4ef
pkgrel=1
_arches=all
arch=(
    x86_64
    aarch64
)
license=(GPL3+)
url=https://gitlab.gnome.org/guidog/phosh-osk-stub
depends=(
    feedbackd
    phoc
    libhandy
)
makedepends=(
    meson
    ninja
)
_commit=f4be4efe622f8ea49c77cc237e7aca316d175e87
source=(
    "git+$url#commit=$_commit"
)
sha256sums=(
    SKIP
)

pkgver() {
    cd "$pkgname"
    git describe --long "$_commit" | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    arch-meson ${pkgname} _build
    # fixme: too high a -j breaks the build
    ninja -j3 -C _build
}

package() {
    DESTDIR="$pkgdir" ninja -C _build install

    install -Dm755 _build/src/phosh-osk-stub \
         "$pkgdir"/usr/bin/phosh-osk-stub
}
