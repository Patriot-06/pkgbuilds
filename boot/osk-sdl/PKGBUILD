# PKGBUILD Kanged from: Danct12 <danct12@disroot.org>
# https://github.com/dreemurrs-embedded/Pine64-Arch.git
# SPDX-License-Identifier: GPL-3.0-or-later
# The license above applies ONLY to this PKGBUILD and its included files and NOT osk-sdl.
_mode=host
pkgname=osk-sdl
pkgver=0.67.1.r0.g41f67e2
pkgrel=3
_arches=all
arch=(
    x86_64
    armv7h
    aarch64
)
license=(GPL3)
depends=(
    device-mapper
    cryptsetup
    sdl2
    sdl2_ttf
    mesa
    ttf-dejavu
)
provides=(osk-sdl)
makedepends=(
    scdoc
    meson
)
_commit=41f67e26b8e8466c0613ea51ff602c19e01c7009

pkgdesc="SDL2 On-screen Keyboard for FDE"

source=(
    "git+https://gitlab.com/postmarketOS/osk-sdl.git#commit=${_commit}"
    osk-sdl-hooks
    osk-sdl-install
    osk-sdl.preset
    50-osk-sdl.conf
)

sha256sums=(
    SKIP
    04c7c1ed9d923d663c3aaf9bfc4638c58f70ff2c78c8a267ec9167a8c62af96b
    8abc3dad58572f6d695d6934bd491ee8e296e718922df8825e7732e5cbbf1229
    07a473bdf1989fb4ad2ed6b708df1ec1c0a88653c67689b7a0c22d392aee0604
    073d90f115ba33100b7124f969c32b7849b1fa268818d24801752dc906cbae9c
)

pkgver() {
    cd "$srcdir"/osk-sdl
    git describe --long --tags --abbrev=7 "$_commit" | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    arch-meson osk-sdl _build
    meson compile -C _build
}

package() {
    DESTDIR="$pkgdir" meson install --no-rebuild -C _build

    # DejaVu is on a different directory than default
    sed -i 's/\/usr\/share\/fonts\/ttf-dejavu/\/usr\/share\/fonts\/TTF/g' ${pkgdir}/etc/osk.conf

    # Install initramfs
    install -Dm644 ${srcdir}/osk-sdl-hooks ${pkgdir}/usr/lib/initcpio/hooks/osk-sdl
    install -Dm644 ${srcdir}/osk-sdl-install ${pkgdir}/usr/lib/initcpio/install/osk-sdl
    install -Dm644 "$srcdir"/osk-sdl.preset "$pkgdir"/etc/mkinitcpio.d/osk-sdl.preset
    install -Dm644 "$srcdir"/50-osk-sdl.conf "$pkgdir"/etc/kupfer/mkinitcpio.conf.d/50-osk-sdl.conf
}
