# SPDX-License-Identifier: GPL-3.0-or-later
# The license above applies ONLY to this PKGBUILD and its included files and NOT unl0kr.
_mode=host
pkgname=unl0kr
pkgver=3.2.0.r0.gedeb5e4
pkgrel=1
_arches=all
arch=(
    x86_64
    armv7h
    aarch64
)
license=(GPL3)
depends=(
    device-mapper
    cryptsetup
    libinput
    libinih
    libxkbcommon
    mesa
    systemd
)
provides=(unl0kr)
conflicts=(osk-sdl)
makedepends=(
    cmake
    linux-headers
    meson
    scdoc
)
_commit=edeb5e4a35494ab2bec7e6215fe86594ea4628ec
_commit_lv_drivers=b8d2a18459ea585e6029ae756295c122569287df

pkgdesc="Framebuffer-based disk unlocker for the initramfs based on LVGL"

source=(
    "git+https://gitlab.com/postmarketOS/buffybox.git#commit=${_commit}"
    "git+https://github.com/calebccff/lv_drivers.git#commit=${_commit_lv_drivers}"
    unl0kr-hooks
    unl0kr-install
    50-unl0kr.conf
)

sha256sums=(
    SKIP
    SKIP
    bed28e5bc358fcc33b815a10dc374c902d565701dfa808bb0fb012822d7072fa
    5e0de6b4eca79f53d7ff57a86a0dbba23c7c5b59eaf54c464080af5ab4490905
    8db97ef8f2a8b18a11394f00654c95a5daf035ede3e9f80bf2b3e7dd2a6c9138
)

pkgver() {
    cd "$srcdir"/buffybox
    git describe --long --tags --abbrev=7 "$_commit" | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd "$srcdir"/buffybox/unl0kr
    mkdir -p lv_drivers
    cp -avf "$srcdir"/lv_drivers/* lv_drivers/
    cd ..
    git submodule update --init --recursive
}

build() {
    cd "$srcdir"/buffybox/unl0kr
    arch-meson build
    meson compile -C build
}

package() {
    cd "$srcdir"/buffybox/unl0kr
    DESTDIR="$pkgdir" meson install --no-rebuild -C build

    # Install initramfs
    install -Dm644 "$srcdir"/unl0kr-hooks "$pkgdir"/usr/lib/initcpio/hooks/unl0kr
    install -Dm644 "$srcdir"/unl0kr-install "$pkgdir"/usr/lib/initcpio/install/unl0kr
    install -Dm644 "$srcdir"/50-unl0kr.conf "$pkgdir"/etc/kupfer/mkinitcpio.conf.d/50-unl0kr.conf

    install -Dm644 "$srcdir"/buffybox/unl0kr/unl0kr.conf "$pkgdir"/etc/unl0kr.conf

    # Added due to pmOS including it. Function is currently undocumented, but it is required
    touch dummy
    install -Dm644 dummy "$pkgdir"/etc/unl0kr.conf.d/dummy
}
