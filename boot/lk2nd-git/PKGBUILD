_mode=cross
pkgbase=boot-lk2nd-git
pkgname=(
    boot-lk2nd-msm8916-git
    boot-lk2nd-msm8974-git
    boot-lk2nd-msm8226-git
)
pkgver=r5705.e9fe6f89
pkgrel=2
_arches=specific
arch=(aarch64)
license=(MIT)
url=https://github.com/msm8916-mainline/lk2nd
provides=(boot-lk2nd)
makedepends=(
    dtc
    arm-none-eabi-gcc
)
_commit=e9fe6f898a3794f1edf9b57013073085010d0494
source=("git+$url#commit=$_commit")
sha256sums=(SKIP)

pkgver() {
    cd "$srcdir"/lk2nd
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd "$srcdir"/lk2nd
    make TOOLCHAIN_PREFIX=arm-none-eabi- msm8916-secondary
    make TOOLCHAIN_PREFIX=arm-none-eabi- msm8974-secondary
    make TOOLCHAIN_PREFIX=arm-none-eabi- msm8226-secondary
}

package_boot-lk2nd-msm8916-git() {
    provides+=(lk2nd-msm8916)
    install -Dm644 "$srcdir"/lk2nd/build-msm8916-secondary/lk2nd.img "${pkgdir}/boot/lk2nd.img"
    install -Dm644 "$srcdir"/lk2nd/build-msm8916-secondary/lk2nd-appended-dtb.img "${pkgdir}/boot/lk2nd-appended-dtb.img"
}

package_boot-lk2nd-msm8974-git() {
    provides+=(lk2nd-msm8974)
    install -Dm644 "$srcdir"/lk2nd/build-msm8974-secondary/lk2nd.img "${pkgdir}/boot/lk2nd.img"
    #install -Dm644 "$srcdir"/lk2nd/build-msm8974-secondary/lk2nd-appended-dtb.img "${pkgdir}/boot/lk2nd-appended-dtb.img"
}

package_boot-lk2nd-msm8226-git() {
    provides+=(lk2nd-msm8226)
    #install -Dm644 "$srcdir"/lk2nd/build-msm8226-secondary/lk2nd.img "${pkgdir}/boot/lk2nd.img"
    install -Dm644 "$srcdir"/lk2nd/build-msm8226-secondary/lk2nd-appended-dtb.img "${pkgdir}/boot/lk2nd-appended-dtb.img"
}
