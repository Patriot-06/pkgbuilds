_mode=cross
pkgbase=boot-lk2nd-git
pkgver=19.0.r15.g2660019
pkgrel=1
_arches=specific
arch=(
    aarch64
    armv7
)
license=(MIT)
url=https://github.com/msm8916-mainline/lk2nd
provides=(boot-lk2nd)
makedepends=(
    python
    dtc
    arm-none-eabi-gcc
)
_commit=26600197fc12852e7e561c72d5240508d3e97305
source=("git+$url#commit=$_commit")
sha256sums=(SKIP)

_targets="msm8916 msm8974 msm8226 msm8952 msm8953"
pkgname=()
for target in $_targets; do pkgname+=("${pkgbase%-git}-$target-git"); done

pkgver() {
    cd "$srcdir"/lk2nd
    git describe --long --abbrev=7 --tags "$_commit" | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    cd "$srcdir"/lk2nd
    rm -rf build-lk2nd-*
    for target in $_targets; do
        make TOOLCHAIN_PREFIX=arm-none-eabi- "lk2nd-$target";
    done
}

_package() {
    local target=${1%%-git}
    pkgdesc="lk2nd for ${target##lk2nd-} platform"
    provides+=("$target")
    install -Dm644 "$srcdir/lk2nd/build-$target/lk2nd.img" "${pkgdir}"/boot/lk2nd.img
}

for p in "${pkgname[@]}"; do
    eval "package_${p}() {
    _package ${p##boot-}
  }"
done
