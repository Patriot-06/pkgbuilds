_mode=cross
pkgname=boot-qhypstub-git
pkgver=r23.4f11478
pkgrel=2
_arches=specific
arch=(aarch64)
license=(GPL2)
url=https://github.com/msm8916-mainline/qhypstub
provides=(boot-qhypstub)
_commit=4f11478a4b24322beddb2bfe9fbceeceac0446e3
_qtestsign_commit=073f0e2f0e445900e6f4e6f14b9caea05d4d62fb
source=(
    "git+$url#commit=$_commit"
    "git+https://github.com/msm8916-mainline/qtestsign#commit=$_qtestsign_commit"
)
sha256sums=(
    SKIP
    SKIP
)

pkgver() {
    cd "$srcdir"/qhypstub
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd "$srcdir"/qhypstub
    LDFLAGS="" make
    ../qtestsign/qtestsign.py hyp qhypstub.elf
}

package() {
    install -Dm644 "$srcdir"/qhypstub/qhypstub.bin "${pkgdir}/boot/qhypstub.bin"
    install -Dm644 "$srcdir"/qhypstub/qhypstub-test-signed.mbn "${pkgdir}/boot/qhypstub-test-signed.mbn"
}
